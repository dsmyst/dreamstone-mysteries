raw`
    .set LOCALID_GRETA, 3
    .set LOCALID_SIZZ, 4
    .set LOCALID_VEN, 5
`

mapscripts MtCeram_Caves_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: MtCeram_Caves_OnTransition
}

script MtCeram_Caves_OnTransition{
    if (flag(FLAG_CHAMPION_GRETA_COMPLETE)) {end}
    if (flag(FLAG_CHAMPION_GRETA_SETUP)){
        setobjectxyperm(6, 30, 43) //greta
        setobjectxyperm(7, 29, 43) //banette
        end
    }    
}

script MtCeram_Caves_Banette{
    lockall
    playmoncry(SPECIES_BANETTE, CRY_MODE_NORMAL)
    msgbox(format("Banette is looking at the wall."))
    releaseall
    end
}

script MtCeram_Caves_Greta_Battle{
    lockall
    call(Common_EventScript_SaveGameBeforeBattle)

    msgbox_delay("Greta", format("I told {PLAYER} to come to where we first met...but was it here?\lOr was it somewhere else?"))

    applymovement(6, moves(emote_exclamation_mark jump_in_place_down))
    applymovement(7, moves(emote_exclamation_mark jump_in_place_down))
    waitmovement(0)

    delay(24)

    msgbox_delay("Greta", format("{PLAYER}! So it was here that we met. You saved me from the Venipede and Sizzlipede!\pYou were just a regular young trainer back then, but now you're the Fifth Cormoria Champion!\pI hope Arsalan does a good job with your painting...\pI haven't been this excited since Ravine and I dueled after I won!\pThe four previous Champions are in a league of their own, {PLAYER}.\lWe've got tricks up our sleeve that you won't see anywhere else!\pAre you ready?"))

    trainerbattle_no_intro(TRAINER_CHAMPION_GRETA, format("Spectacular!"))

    msgbox_delay("Greta", format("This feeling...I've been so focused on business, I forgot what it was like to be a trainer and battle! The adrenaline! Oh Banette, I need to hold on to you.\pAhem.\p{PLAYER} - that was incredible!\nYou're really worth your title.\pSigh...I feel like asking for a rematch, but I can't leave my stall unmanned for this long. I suppose my battling days are behind me now.\pSee you around, {PLAYER}!"))
    applymovement(6, moves(walk_left walk_down * 2 walk_left * 7))
    applymovement(7, moves(walk_left walk_right walk_down * 2 walk_left * 7))
    waitmovement(0)

    removeobject(6)
    removeobject(7)

    setflag(FLAG_CHAMPION_GRETA_COMPLETE)
    clearflag(FLAG_HIDE_CHAMPION_GRETA)

    releaseall
    end

}



script MtCeram_Caves_Greta_Trigger0{
    if (flag(FLAG_CERAM_CAVES_GRETA_MEETING)) {end}

    lockall
    playbgm(MUS_ENCOUNTER_TWINS, FALSE)

    applymovement(LOCALID_GRETA, moves(emote_exclamation_mark))
    waitmovement(0)
    applymovement(LOCALID_SIZZ, moves(jump_in_place_right))
    applymovement(LOCALID_VEN, moves(jump_in_place_left))
    speakername("???")
    msgbox(format("Help!"))
    release

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up walk_left * 3 walk_up * 2))
    waitmovement(0)

    goto(MtCeram_Caves_Greta_Main)
}

script MtCeram_Caves_Greta_Trigger1{
    if (flag(FLAG_CERAM_CAVES_GRETA_MEETING)) {end}

    lockall
    playbgm(MUS_ENCOUNTER_TWINS, FALSE)
    applymovement(LOCALID_GRETA, moves(emote_exclamation_mark))
    waitmovement(0)
    applymovement(LOCALID_SIZZ, moves(jump_in_place_right))
    applymovement(LOCALID_VEN, moves(jump_in_place_left))
    speakername("???")
    msgbox(format("Help!"))
    release

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up walk_left * 4 walk_up * 2))
    waitmovement(0)

    goto(MtCeram_Caves_Greta_Main)
}

script MtCeram_Caves_Greta_Trigger2{
    if (flag(FLAG_CERAM_CAVES_GRETA_MEETING)) {end}
    
    lockall
    playbgm(MUS_ENCOUNTER_TWINS, FALSE)
    applymovement(LOCALID_GRETA, moves(emote_exclamation_mark))
    waitmovement(0)
    playmoncry(SPECIES_SIZZLIPEDE, CRY_MODE_NORMAL)
    applymovement(LOCALID_SIZZ, moves(jump_in_place_right))
    applymovement(LOCALID_VEN, moves(jump_in_place_left))
    waitmovement(0)
    speakername("???")
    msgbox(format("Help!"))
    release

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up walk_left * 5 walk_up * 2))
    waitmovement(0)

    goto(MtCeram_Caves_Greta_Main)
}


//	.macro setwildbattle species:req, level:req, item=ITEM_NONE, species2=SPECIES_NONE, level2=0, item2=ITEM_NONE

script MtCeram_Caves_Greta_Main{
    speakername("???")
    msgbox(format("I'm trapped in between this Sizzlipede and Venipede who are fighting each other! I can't get free!"))
    release 
    delay(24)
    playmoncry(SPECIES_SIZZLIPEDE, CRY_MODE_ENCOUNTER)
    playmoncry(SPECIES_VENIPEDE, CRY_MODE_ENCOUNTER)    
    applymovement(LOCALID_SIZZ, moves(face_down emote_exclamation_mark))
    applymovement(LOCALID_VEN, moves(face_down emote_exclamation_mark))
    waitmovement(0)
    delay(30)
    speakername("???")
    msgbox(format("Uh oh, they've noticed you..."))
    release
    applymovement(LOCALID_SIZZ, moves(walk_down face_right))
    applymovement(LOCALID_VEN, moves(walk_down face_left))
    waitmovement(0)    

    setwildbattle(SPECIES_SIZZLIPEDE, 17, ITEM_NONE, SPECIES_VENIPEDE, 17, ITEM_NONE)
    dowildbattle

    removeobject(LOCALID_SIZZ)
    removeobject(LOCALID_VEN)
    speakername("???")
    msgbox(format("Thank you so much! I forgot my Pok√©mon at home and couldn't do anything! People at the Ceram Base Camp told me that the Sizzlipede and Venipede fight all the time...that's what I get for not taking them seriously!"))
    speakername("Greta")
    msgbox(format("I'm Greta. I'm a traveling merchant!\pAre you a trainer? What's your name?\p...\p{PLAYER}? Nice to meet you!\nI have to thank you for saving me.\pLets see..."))
    release
    delay(16)
    speakername("Greta")
    msgbox(format("I have some leftover stock that you might like. These gems came from Mt. Mirroh, on the other side of Cormoria!\pOnce per battle, a gem increases the power of its corresponding move type by a third. Take any one you like!"))
    release
    
    delay(24)
    dynmultichoice(0,0, TRUE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Dark Gem", "Fighting Gem", "Psychic Gem")
    if (var(VAR_RESULT) == 0){
        giveitem(ITEM_DARK_GEM)
    } 
    else 
    {
        if (var(VAR_RESULT) == 1){
            giveitem(ITEM_FIGHTING_GEM)
        } 
        else {
            if (var(VAR_RESULT) == 2){
                giveitem(ITEM_PSYCHIC_GEM)
            }
        }
    } 

    speakername("Greta")
    msgbox(format("I travel across all the routes and towns of Cormoria, so I bet we'll meet again - somewhere or the other.\lPlease buy my stuff then!"))
    release
    setflag(FLAG_CERAM_CAVES_GRETA_MEETING)

    applymovement(LOCALID_GRETA, moves(walk_left walk_down * 2 walk_left * 7))
    waitmovement(0)
    removeobject(LOCALID_GRETA)

    fadedefaultbgm
    releaseall

    end    


}